defmodule PhoenixKitWeb.Components.Core.Flash do
  @moduledoc """
  Provides flash UI components.
  """

  use Phoenix.Component
  use Gettext, backend: PhoenixKitWeb.Gettext

  alias Phoenix.LiveView.JS

  import PhoenixKitWeb.Components.Core.Icon, only: [icon: 1]

  @doc """
  Renders flash notices.

  ## Examples

      <.flash kind={:info} flash={@flash} />
      <.flash kind={:info} phx-mounted={show("#flash")}>Welcome Back!</.flash>
  """
  attr :id, :string, doc: "the optional id of flash container"
  attr :flash, :map, default: %{}, doc: "the map of flash messages to display"
  attr :title, :string, default: nil
  attr :kind, :atom, values: [:info, :warning, :error], doc: "used for styling and flash lookup"
  attr :autoclose, :any, default: 5000, doc: "Auto-dismiss delay in ms, or false to disable"
  attr :rest, :global, doc: "the arbitrary HTML attributes to add to the flash container"

  slot :inner_block, doc: "the optional inner block that renders the flash message"

  def flash(assigns) do
    assigns = assign_new(assigns, :id, fn -> "flash-#{assigns.kind}" end)

    ~H"""
    <div
      :if={msg = render_slot(@inner_block) || Phoenix.Flash.get(@flash, @kind)}
      id={@id}
      phx-hook={@autoclose && "FlashAutoDismiss"}
      data-dismiss-after={@autoclose && @autoclose}
      data-flash-kind={@autoclose && @kind}
      data-flash-message={@autoclose && msg}
      phx-click={JS.push("lv:clear-flash", value: %{key: @kind}) |> hide_flash("##{@id}")}
      role="alert"
      class="toast toast-top toast-end z-[1000]"
      {@rest}
    >
      <div class={[
        "alert w-80 sm:w-96 max-w-80 sm:max-w-96 text-wrap relative",
        @kind == :info && "alert-info",
        @kind == :warning && "alert-warning",
        @kind == :error && "alert-error"
      ]}>
        <.icon :if={@kind == :info} name="hero-information-circle" class="size-5 shrink-0" />
        <.icon :if={@kind == :warning} name="hero-exclamation-triangle" class="size-5 shrink-0" />
        <.icon :if={@kind == :error} name="hero-exclamation-circle" class="size-5 shrink-0" />
        <div>
          <p :if={@title} class="font-semibold">{@title}</p>
          <p class="whitespace-pre-line">{msg}</p>
        </div>
        <div class="flex-1" />
        <button type="button" class="group self-start cursor-pointer" aria-label={gettext("close")}>
          <.icon name="hero-x-mark" class="size-5 opacity-40 group-hover:opacity-70" />
        </button>
        <%= if @autoclose do %>
          <div class="absolute bottom-0 left-0 right-0 h-0.5 overflow-hidden rounded-b-box">
            <div
              data-flash-progress
              class={[
                "h-full",
                @kind == :info && "bg-info-content/30",
                @kind == :warning && "bg-warning-content/30",
                @kind == :error && "bg-error-content/30"
              ]}
              style="width: 100%"
            />
          </div>
        <% end %>
      </div>
    </div>
    """
  end

  @doc """
  Shows the flash group with all flash messages.

  ## Examples

      <.flash_group flash={@flash} />
  """
  attr :flash, :map, required: true, doc: "the map of flash messages"
  attr :id, :string, default: "flash-group", doc: "the optional id of flash container"

  def flash_group(assigns) do
    ~H"""
    <div id={@id}>
      <.flash kind={:info} title={gettext("Success!")} flash={@flash} />
      <.flash kind={:warning} title={gettext("Note")} flash={@flash} />
      <.flash kind={:error} title={gettext("Error!")} flash={@flash} autoclose={8000} />
    </div>
    """
  end

  defp hide_flash(js, selector) do
    JS.hide(js,
      to: selector,
      time: 200,
      transition:
        {"transition-all transform ease-out duration-200",
         "opacity-100 translate-y-0 sm:translate-x-0", "opacity-0 translate-y-2 sm:translate-x-2"}
    )
  end
end
